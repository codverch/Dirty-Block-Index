# Copyright (c) 2017, TU Dresden
# Copyright (c) 2017, University of Kaiserslautern
# All rights reserved.

# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included in
# all copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.
#
# Authors: Christian Menard
#          Matthias Jung

import os

Import('main')

main.Prepend(CPPPATH=Dir('./src'))
main.Prepend(CPATH=Dir('./src'))

main.Prepend(CXXFLAGS=['-DSC_INCLUDE_FX', '-pthread'])
main.Prepend(CFLAGS=['-DSC_INCLUDE_FX', '-pthread'])

conf = Configure(main)

if main['PLATFORM'] == 'darwin':
    main.Append(LINKFLAGS=['-undefined', 'dynamic_lookup'])

s_file = None
if conf.CheckDeclaration("__i386__"):
    s_file = 'i386.s'
if conf.CheckDeclaration("__x86_64__"):
    s_file = 'iX86_64.s'
conf.Finish()

if s_file is None:
    print 'Unsupported CPU architecture!'
    Exit(1)

systemc_files = Glob('src/sysc/kernel/*.cpp')
systemc_files += ['src/sysc/qt/qt.c', 'src/sysc/qt/md/' + s_file]
systemc_files += Glob('src/sysc/communication/*.cpp')
systemc_files += Glob('src/sysc/tracing/*.cpp')
systemc_files += Glob('src/sysc/utils/*.cpp')
systemc_files += Glob('src/sysc/datatypes/bit/*.cpp')
systemc_files += Glob('src/sysc/datatypes/fx/*.cpp')
systemc_files += Glob('src/sysc/datatypes/int/*.cpp')
systemc_files += Glob('src/sysc/datatypes/misc/*.cpp')

main.Library('libsystemc', systemc_files)
main.SharedLibrary('libsystemc', systemc_files)

